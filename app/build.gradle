plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {
    signingConfigs {
        ocr {
            keyAlias 'ocrlite'
            keyPassword 'ocrlite'
            storeFile file('../keystore/ocrlite.keystore')
            storePassword 'ocrlite'
        }
    }

    compileSdkVersion compile_sdk_version

    defaultConfig {
        applicationId "com.benjaminwan.ocr.ncnn"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode project_version_code
        versionName project_version_name
        signingConfig signingConfigs.ocr
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'ocr_library_types'
    productFlavors {
        cpu {
            minSdkVersion 21
        }
        gpu {
            minSdkVersion 24
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    kapt {
        correctErrorTypes = true
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "${rootProject.name}-${defaultConfig.versionName}-${variant.flavorName}-${variant.buildType.name}.apk"
            }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //coroutines
    def coroutines_version = '1.6.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //Android
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    //lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    implementation project(":OcrLibrary")

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    def epoxyVersion = '4.6.3'
    implementation "com.airbnb.android:epoxy:$epoxyVersion"
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"

    // CameraX core library
    def camerax_version = '1.1.0-beta03'
    implementation "androidx.camera:camera-core:$camerax_version"
    // CameraX Camera2 extensions
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:$camerax_version"

    //cameraMask 相机遮罩
    implementation 'jsc.kit.cameramask:camera-mask:0.3.0'

    //runWithPermissions 申请权限
    implementation 'com.afollestad.assent:core:3.0.0-RC4'
    implementation 'com.afollestad.assent:rationales:3.0.0-RC4'

    //Logger
    implementation 'com.orhanobut:logger:2.2.0'

    implementation(name: 'IdCardView-1.0', ext: 'aar')
}